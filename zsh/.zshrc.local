source $HOME/.zsh-autosuggestions.zsh

autoload -U edit-command-line
zle -N edit-command-line
bindkey '^xe' edit-command-line
bindkey '^x^e' edit-command-line

alias free='free -m'
alias vi='vim'
alias wget='wget -c'
alias t='tree -C --dirsfirst'
alias ax='simple-extract'
alias g='git'
alias lg='lazygit'

export VISUAL="vim"

## Mark-Unmark-Jump 
## http://jeroenjanssens.com/2013/08/16/quickly-navigate-your-filesystem-from-the-command-line.html 
alias j='jump'
export MARKPATH=$HOME/.marks
function jump { 
    cd -P $MARKPATH/$1 2>/dev/null || echo "No such mark: $1"
}
function mark { 
    mkdir -p $MARKPATH; ln -s $(pwd) $MARKPATH/$1
}
function unmark { 
    rm -i $MARKPATH/$1 
}
function marks {
    ls -l $MARKPATH | sed 's/  / /g' | cut -d' ' -f9- | sed 's/ -/\t-/g' && echo
}
function _compmarks {
    reply=($(ls $MARKPATH))
}
compctl -K _compmarks jump
compctl -K _compmarks unmark

# Virtualenv support
function virtual_env_prompt () {
    REPLY=${VIRTUAL_ENV+(${VIRTUAL_ENV:t}) }
}
grml_theme_add_token  virtual-env -f virtual_env_prompt '%F{magenta}' '%f'

function toolbox_env_prompt () {
    if [ -f /run/.containerenv ] && [ -f /run/.toolboxenv ]; then
        local marker=("â¬¢ ") 
    fi
    REPLY="$marker"
}
grml_theme_add_token toolbox-env -f toolbox_env_prompt '%F{magenta}' '%f'

zstyle ':prompt:grml:left:setup' items rc toolbox-env virtual-env change-root path vcs percent 

## alert me if something failed
setopt printexitvalue

## changed completer settings
zstyle ':completion:*' completer _complete _correct _approximate
zstyle ':completion:*' expand prefix suffix
     
## Find out which libs define a symbol
lcheck() {
    if [[ -n "$1" ]] ; then
        nm -go /usr/lib/lib*.a 2>/dev/null | grep ":[[:xdigit:]]\{8\} . .*$1"
    else
        echo "Usage: lcheck <function>" >&2
    fi
}

## Download a file and display it locally
uopen() {
    emulate -L zsh
    if ! [[ -n "$1" ]] ; then
        print "Usage: uopen \$URL/\$file">&2
        return 1
    else
        FILE=$1
        MIME=$(curl --head $FILE | \
               grep Content-Type | \
               cut -d ' ' -f 2 | \
               cut -d\; -f 1)
        MIME=${MIME%$'\r'}
        curl $FILE | see ${MIME}:-
    fi
}

## Memory overview
memusage() {
    ps aux | awk '{if (NR > 1) print $5;
                   if (NR > 2) print "+"}
                  END { print "p" }' | dc
}

## print hex value of a number
hex() {
    emulate -L zsh
    if [[ -n "$1" ]]; then
        printf "%x\n" $1
    else
        print 'Usage: hex <number-to-convert>'
        return 1
    fi
}

## ctrl-s will no longer freeze the terminal.
stty erase "^?"

## Auto-start tmux
# Set TMUX_AUTO to true to auto start tmux
function start_tmux() {
    if type tmux &> /dev/null; then
        #if not inside a tmux session, and if no session is started, start a new session
        if [[ $TMUX_AUTO == true && -z "$TMUX" && -z $TERMINAL_CONTEXT  ]]; then
            (tmux -2 attach || tmux -2 new-session)
        fi
    fi
}

## Load machine specific env variables and aliases
DEFAULT_ENV=${HOME}/.zsh-env/linux.sh
MACHINE_ENV=${HOME}/.zsh-env/$(hostname).sh
if [[ -r $MACHINE_ENV ]]; then
    source $MACHINE_ENV
elif [[ -r $DEFAULT_ENV ]]; then
    source $DEFAULT_ENV
fi 

start_tmux

